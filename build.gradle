
apply plugin: 'scala'

compileScala.options.encoding = 'UTF-8'
// fix scala+java mix to all use compileScala which uses correct dependency order
sourceSets.main.scala.srcDir 'src/main/scala/aqp'
sourceSets.main.java.srcDirs = ['src/test/java/app', 'gemfire-core/src/main/java']
sourceSets.test.java.srcDirs = []
sourceSets.test.scala.srcDirs = [ 'src/test/java', 'src/test/scala',
                                    'src/dunit/java', 'src/dunit/scala' ,'src/test/scala/aqp']

ext {
  PRODUCT_NAME = 'SnappyData AQP'
  createdBy = System.getProperty('user.name')
  if (rootProject.hasProperty('enablePublish')) {
    createdBy = vendorName
  }
  osArch = System.getProperty('os.arch')
  osName = org.gradle.internal.os.OperatingSystem.current()
  osVersion = System.getProperty('os.version')
  buildDate = new Date().format('yyyy-MM-dd HH:mm:ss Z')
  buildNumber = new Date().format('MMddyy')
  jdkVersion = System.getProperty('java.version')
  gitCmd = "git --git-dir=${project.projectDir}/.git --work-tree=${project.projectDir}"
  gitBranch = "${gitCmd} rev-parse --abbrev-ref HEAD".execute().text.trim()
  commitId = "${gitCmd} rev-parse HEAD".execute().text.trim()
  sourceDate = "${gitCmd} log -n 1 --format=%ai".execute().text.trim()
  clusterType = ''
}

dependencies {
  compile 'org.scala-lang:scala-library:' + scalaVersion
  compile 'org.scala-lang:scala-reflect:' + scalaVersion

  runtime project(':snappy-cluster_' + scalaBinaryVersion)
  compile (project(':snappy-core_' + scalaBinaryVersion)) {
    exclude(group: 'org.apache.spark', module: 'spark-unsafe_' + scalaBinaryVersion)
    exclude(group: 'org.apache.spark', module: 'spark-core_' + scalaBinaryVersion)
    exclude(group: 'org.apache.spark', module: 'spark-catalyst_' + scalaBinaryVersion)
    exclude(group: 'org.apache.spark', module: 'spark-sql_' + scalaBinaryVersion)
    exclude(group: 'org.apache.spark', module: 'spark-hive_' + scalaBinaryVersion)
    exclude(group: 'org.apache.spark', module: 'spark-streaming_' + scalaBinaryVersion)
    exclude(group: 'org.apache.spark', module: 'spark-mllib_' + scalaBinaryVersion)
    exclude(group: 'org.eclipse.jetty', module: 'jetty-servlet')
  }
  if (new File(rootDir, 'spark/build.gradle').exists()) {
    compile project(':snappy-spark:snappy-spark-repl_' + scalaBinaryVersion)
    compile project(':snappy-spark:snappy-spark-yarn_' + scalaBinaryVersion)
    compile project(':snappy-spark:snappy-spark-graphx_' + scalaBinaryVersion)
    compile project(':snappy-spark:snappy-spark-hive-thriftserver_' + scalaBinaryVersion)
  } else {
    compile 'io.snappydata:snappy-spark-repl_' + scalaBinaryVersion + ':' + snappySparkVersion
    compile 'io.snappydata:snappy-spark-yarn_' + scalaBinaryVersion + ':' + snappySparkVersion
    compile 'io.snappydata:snappy-spark-graphx_' + scalaBinaryVersion + ':' + snappySparkVersion
    compile 'io.snappydata:snappy-spark-hive-thriftserver_' + scalaBinaryVersion + ':' + snappySparkVersion
  }
  if (new File(rootDir, 'store/build.gradle').exists()) {
    testCompile project(':snappy-store:snappydata-store-client')
    testCompile project(':snappy-store:snappydata-store-core')
    testCompile project(':snappy-store:snappydata-store-tools')
    testCompile project(path: ':snappy-store:snappydata-store-tools', configuration: 'testOutput')
  } else {
    testCompile group: 'io.snappydata', name: 'snappydata-store-client', version: snappyStoreVersion
    testCompile group: 'io.snappydata', name: 'snappydata-store-core', version: snappyStoreVersion
    testCompile group: 'io.snappydata', name: 'snappydata-store-tools', version: snappyStoreVersion
    testCompile group: 'io.snappydata', name: 'snappydata-store-tools', version: snappyStoreVersion, classifier: 'tests'
  }
  testRuntime (project(':snappy-core_' + scalaBinaryVersion)) {
    exclude(group: 'org.apache.spark', module: 'spark-unsafe_' + scalaBinaryVersion)
    exclude(group: 'org.apache.spark', module: 'spark-core_' + scalaBinaryVersion)
    exclude(group: 'org.apache.spark', module: 'spark-catalyst_' + scalaBinaryVersion)
    exclude(group: 'org.apache.spark', module: 'spark-sql_' + scalaBinaryVersion)
    exclude(group: 'org.apache.spark', module: 'spark-hive_' + scalaBinaryVersion)
    exclude(group: 'org.apache.spark', module: 'spark-streaming_' + scalaBinaryVersion)
    exclude(group: 'org.apache.spark', module: 'spark-streaming-kafka-0-10_' + scalaBinaryVersion)
    exclude(group: 'org.apache.spark', module: 'spark-sql-kafka-0-10_' + scalaBinaryVersion)
    exclude(group: 'org.apache.spark', module: 'spark-mllib_' + scalaBinaryVersion)
    exclude(group: 'org.eclipse.jetty', module: 'jetty-servlet')
  }
  testRuntime project(':snappy-cluster_' + scalaBinaryVersion)
  testCompile project(':dunit')
  testCompile (project(path: ':snappy-core_' + scalaBinaryVersion, configuration: 'testOutput')) {
    exclude(group: 'org.apache.spark', module: 'spark-unsafe_' + scalaBinaryVersion)
    exclude(group: 'org.apache.spark', module: 'spark-core_' + scalaBinaryVersion)
    exclude(group: 'org.apache.spark', module: 'spark-catalyst_' + scalaBinaryVersion)
    exclude(group: 'org.apache.spark', module: 'spark-sql_' + scalaBinaryVersion)
    exclude(group: 'org.apache.spark', module: 'spark-hive_' + scalaBinaryVersion)
    exclude(group: 'org.apache.spark', module: 'spark-streaming_' + scalaBinaryVersion)
    exclude(group: 'org.apache.spark', module: 'spark-streaming-kafka-0-10_' + scalaBinaryVersion)
    exclude(group: 'org.apache.spark', module: 'spark-sql-kafka-0-10_' + scalaBinaryVersion)
    exclude(group: 'org.apache.spark', module: 'spark-mllib_' + scalaBinaryVersion)
    exclude(group: 'org.eclipse.jetty', module: 'jetty-servlet')
  }
  testRuntime project(':snappy-core_' + scalaBinaryVersion)
  testCompile project(':snappy-cluster_' + scalaBinaryVersion)
  testCompile project(':dunit')
  testCompile project(path: ':snappy-core_' + scalaBinaryVersion, configuration: 'testOutput')
  testCompile project(path: ':snappy-cluster_' + scalaBinaryVersion, configuration: 'testOutput')
  testCompile "org.scalatest:scalatest_${scalaBinaryVersion}:${scalatestVersion}"
  testRuntime files("${projectDir}/../tests/common/src/main/resources")
  testRuntime "org.pegdown:pegdown:${pegdownVersion}"
}


// Creates the version properties file and writes it to the resources dir
task createVersionPropertiesFile(dependsOn: 'processResources') {
  def propertiesDir = file("${sourceSets.main.scala.outputDir}/io/snappydata")
  outputs.file "${propertiesDir}/SnappyAQPVersion.properties"
  inputs.file "${rootProject.projectDir}/build.gradle"
  if (rootProject.hasProperty('clusterType')) {
    clusterType = rootProject.property('clusterType')
  }

  doLast {

    def props = [
      'Product-Name'      : PRODUCT_NAME,
      'Product-Version'   : version,
      'Build-Id'          : buildIdPrefix + buildNumber,
      'Build-Date'        : buildDate,
      'Build-Platform'    : osName.getName() + osVersion + osArch,
      'Build-Java-Version': jdkVersion,
      'Source-Date'       : sourceDate,
      'Source-Revision'   : commitId,
      'Source-Repository' : gitBranch,
      'Cluster-Type'      : clusterType
    ]

    writeProperties(propertiesDir, 'SnappyAQPVersion.properties',
        "Properties that control what version ${PRODUCT_NAME} will think it is. Changing these values may cause ${PRODUCT_NAME} to no longer function.", props)
  }
}

compileJava {
  dependsOn createVersionPropertiesFile
  doLast {
    // copy native JNI libraries to snappy-spark-unsafe output
    if (new File(rootDir, 'spark/build.gradle').exists()) {
      def unsafeProject = project(":snappy-spark:snappy-spark-unsafe_${scalaBinaryVersion}")
      copy {
        from "${projectDir}/lib"
        into "${unsafeProject.buildDir}/libs"
      }
      copy {
        from "${projectDir}/lib"
        into "${unsafeProject.sourceSets.main.java.outputDir}/.."
      }
    }
  }
}

artifacts {
  archives packageTests
}


archivesBaseName = 'snappydata-aqp-scala-tests'

testClasses.doLast {
  if (new File(rootDir, 'store/build.gradle').exists()) {
    copy {
      from ("src/test/java"){
        include '**/*.bt'
        include '**/*.conf'
        include '**/*.sql'
      }
      into project(':snappy-store:snappydata-store-tests').sourceSets.main.java.outputDir
    }
  }
}

scalaTest {
  dependsOn ':cleanScalaTest'
  doFirst {
    // cleanup files since scalatest plugin does not honour workingDir yet
    cleanIntermediateFiles(project.path)
  }
  doLast {
    // cleanup files since scalatest plugin does not honour workingDir yet
    cleanIntermediateFiles(project.path)
  }
}

test.dependsOn ':cleanJUnit'
check.dependsOn.clear()
check.dependsOn test, scalaTest, dunitTest

archivesBaseName = 'snappydata-aqp_' + scalaBinaryVersion
